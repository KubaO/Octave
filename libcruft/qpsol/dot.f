      DOUBLE PRECISION FUNCTION DOT( N, X, LENX, INCX, Y, LENY, INCY )
C
      INTEGER            N, LENX, INCX, LENY, INCY
      DOUBLE PRECISION   X(LENX), Y(LENY)
C
      DOUBLE PRECISION   WMACH
      COMMON    /SOLMCH/ WMACH(15)
C
C  DOT  RETURNS THE INNER PRODUCT OF X AND Y.
C  VERSION OF FEBRUARY 1981.  SIMPLE CASE ADDED MARCH 1983.
C
      INTEGER            I, IX, IY, NINCX
      DOUBLE PRECISION   ABSXI, FLMIN, ONE, UNDFLW, ZERO
      DOUBLE PRECISION   DABS
      DATA               ZERO, ONE/0.0D+0, 1.0D+0/
C
      DOT = ZERO
      IF (N .LT. 1) RETURN
      IX = 1
      IY = 1
      UNDFLW = WMACH(9)
      IF (UNDFLW .GT. ZERO) GO TO 110
C
C  NO UNDERFLOW TEST REQUIRED.
C  DO THE MOST COMMON CASE SPECIALLY (INCX = INCY).
C
      IF (INCX .NE. INCY) GO TO 50
      NINCX  = N * INCX
      DO 40 I = 1, NINCX, INCX
         DOT  = DOT + X(I)*Y(I)
   40 CONTINUE
      RETURN
C
   50 DO 100 I = 1, N
         DOT = DOT + X(IX)*Y(IY)
         IX  = IX + INCX
         IY  = IY + INCY
  100 CONTINUE
      RETURN
C
C  GUARD AGAINST UNDERFLOW.
C
  110 FLMIN = WMACH(5)
      DO 140 I = 1, N
         ABSXI = DABS(X(IX))
         IF (ABSXI .EQ. ZERO) GO TO 130
         IF (ABSXI .GE. ONE)  GO TO 120
         IF (DABS(Y(IY)) .LT. FLMIN/ABSXI) GO TO 130
  120    DOT = DOT + X(IX)*Y(IY)
  130    IX  = IX + INCX
         IY  = IY + INCY
  140 CONTINUE
      RETURN
C
C  END OF DOT
      END
