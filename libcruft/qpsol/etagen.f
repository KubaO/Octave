      SUBROUTINE ETAGEN( N, ALPHA, X, LENX, INCX, ISWAP, ITRANS )
C
      INTEGER            N, LENX, INCX, ISWAP, ITRANS
      DOUBLE PRECISION   ALPHA
      DOUBLE PRECISION   X(LENX)
C
      DOUBLE PRECISION   WMACH
      COMMON    /SOLMCH/ WMACH(15)
C
C  *********************************************************************
C  ETAGEN  GENERATES AN ELIMINATION TRANSFORMATION  E  SUCH THAT
C
C     E ( ALPHA )  =  ( DELTA ) ,
C       (   X   )     (   0   )
C
C  WHERE  E  HAS THE FORM
C
C     E  = ( 1    ) P
C          ( Z  I )
C
C  FOR SOME N-VECTOR  Z  AND PERMUTATION MATRIX  P  OF ORDER  N + 1.
C
C  IN CERTAIN CIRCUMSTANCES ( X  VERY SMALL IN ABSOLUTE TERMS OR
C  X  VERY SMALL COMPARED TO  ALPHA),  E  WILL BE THE IDENTITY MATRIX.
C  ETAGEN  WILL THEN LEAVE  ALPHA  AND  X  UNALTERED, AND WILL RETURN
C  ISWAP = 0,  ITRANS = 0.
C
C  MORE GENERALLY,  ISWAP  AND  ITRANS  INDICATE THE VARIOUS POSSIBLE
C  FORMS OF  P  AND  Z  AS FOLLOWS.
C
C  IF  ISWAP  =  0,  P = I.
C  IF  ISWAP  GT 0,  P  INTERCHANGES  ALPHA  AND  X(ISWAP).
C
C  IF  ITRANS =  0,  Z = 0  AND THE TRANSFORMATION IS JUST  E = P.
C  IF  ITRANS GT 0,  Z  IS NONZERO.  ITS ELEMENTS ARE RETURNED IN  X.
C
C  ETAGEN  GUARDS AGAINST OVERFLOW AND UNDERFLOW.
C  IT IS ASSUMED THAT  FLMIN .LT. EPSMCH**2  (I.E.  RTMIN .LT. EPSMCH).
C
C  VERSION 1, MARCH 31 1983.
C  SYSTEMS OPTIMIZATION LABORATORY, STANFORD UNIVERSITY.
C  *********************************************************************
C
      INTEGER            I, IMAX, NINCX, NZERO
      DOUBLE PRECISION   ABSALF, EPSMCH, RTMIN, TOL, XMAX, ZERO
      DOUBLE PRECISION   DABS
      DATA               ZERO/0.0D+0/
C
      ISWAP  = 0
      ITRANS = 0
      IF (N .LT. 1) RETURN
      EPSMCH = WMACH(3)
      RTMIN  = WMACH(6)
      ABSALF = DABS(ALPHA)
      XMAX   = ZERO
      NINCX  = N * INCX
C
      DO 10 I = 1, NINCX, INCX
         IF (XMAX  .GE.  DABS( X(I) )) GO TO 10
         XMAX   = DABS( X(I) )
         IMAX   = I
   10 CONTINUE
C
C  EXIT IF  X  IS VERY SMALL.
C
      IF (XMAX .LE. RTMIN) RETURN
C
C  SEE IF AN INTERCHANGE IS NEEDED FOR STABILITY.
C
      IF (ABSALF .LT. XMAX) ISWAP = IMAX
      IF (ISWAP  .EQ.    0) GO TO 200
      XMAX    = X(IMAX)
      X(IMAX) = ALPHA
      ALPHA   = XMAX
C
C  FORM THE MULTIPLIERS IN  X.  THEY WILL BE NO GREATER THAN ONE
C  IN MAGNITUDE.  CHANGE NEGLIGIBLE MULTIPLIERS TO ZERO.
C
  200 TOL    = DABS( ALPHA ) * EPSMCH
      NZERO  = 0
C
      DO 300 I = 1, NINCX, INCX
         IF (DABS( X(I) ) .LE. TOL) GO TO 250
         X(I)  = - X(I) / ALPHA
         GO TO 300
C
  250    X(I)  = ZERO
         NZERO = NZERO + 1
  300 CONTINUE
C
C  Z  IS ZERO ONLY IF  NZERO = N.
C
      IF (NZERO .LT. N) ITRANS = 1
      RETURN
C
C  END OF ETAGEN
      END
