C+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
C     FILE QPSUBS66 FORTRAN
C
C     QPCHKP   QPCOLR   QPCORE   QPCRSH   QPDUMP   QPGRAD   QPPRT
C+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      SUBROUTINE QPCHKP( N, NCLIN, NCLIN0, ISSAVE, JDSAVE, AP, P )
C
C     IMPLICIT           REAL*8(A-H,O-Z)
      INTEGER            N, NCLIN, NCLIN0, ISSAVE, JDSAVE
      DOUBLE PRECISION   AP(NCLIN0), P(N)
C
      INTEGER            NOUT, MSG, ISTART
      COMMON    /SOL1CM/ NOUT, MSG, ISTART
C
C  *********************************************************************
C  QPCHKP  IS CALLED WHEN A CONSTRAINT HAS JUST BEEN DELETED AND THE
C  SIGN OF THE SEARCH DIRECTION  P  MAY BE INCORRECT BECAUSE OF ROUNDING
C  ERRORS IN THE COMPUTATION OF THE PROJECTED GRADIENT  ZTG.  THE SIGN
C  OF THE SEARCH DIRECTION (AND THEREFORE THE PRODUCT  AP)  IS FIXED BY
C  FORCING  P  TO SATISFY THE CONSTRAINT (WITH INDEX  JDSAVE)  THAT WAS
C  JUST DELETED.  VARIABLES THAT WERE HELD TEMPORARILY FIXED (WITH
C  ISTATE = 4)  ARE NOT CHECKED FOR FEASIBILITY.
C
C  SYSTEMS OPTIMIZATION LABORATORY, STANFORD UNIVERSITY.
C  ORIGINAL VERSION DECEMBER 1982.
C  *********************************************************************
C
      INTEGER            IDEL
      DOUBLE PRECISION   ATP, ONE, ZERO
      DATA               ZERO, ONE /0.0D+0, 1.0D+0/
C
      IF (ISSAVE .EQ. 4) GO TO 900
C
      IDEL = JDSAVE - N
      IF (JDSAVE .LE. N) ATP =  P(JDSAVE)
      IF (JDSAVE .GT. N) ATP = AP(IDEL)
C
      IF (MSG .GE. 80) WRITE (NOUT, 1000) JDSAVE, ISSAVE, ATP
C
      IF (      ISSAVE .EQ. 2  .AND.  ATP .LE. ZERO
     *    .OR.  ISSAVE .EQ. 1  .AND.  ATP .GE. ZERO) GO TO 900
C
C  REVERSE THE DIRECTION OF  P  AND  AP.
C
      CALL SSCALE(     N, (- ONE),  P,     N, 1 )
      IF (NCLIN .GT. 0)
     *CALL SSCALE( NCLIN, (- ONE), AP, NCLIN, 1 )
C
  900 RETURN
C
 1000 FORMAT(/ 42H //QPCHKP //  JDSAVE ISSAVE            ATP
     *       / 13H //QPCHKP // , 2I7, G15.5 )
C
C  END OF QPCHKP
      END
