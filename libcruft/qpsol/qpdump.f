      SUBROUTINE QPDUMP( N, NROWH, NCOLH,
     *                   CVEC, HESS, QPHESS, WRK, HX )
C
C     IMPLICIT           REAL*8(A-H,O-Z)
      EXTERNAL           QPHESS
      INTEGER            N, NROWH, NCOLH
      DOUBLE PRECISION   CVEC(N), HESS(NROWH,NCOLH), WRK(N), HX(N)
C
      INTEGER            NOUT, MSG, ISTART
      COMMON    /SOL1CM/ NOUT, MSG, ISTART
C
C  *********************************************************************
C  QPDUMP  PRINTS QUANTITIES DEFINING THE QUADRATIC FUNCTION GIVEN
C  TO  QPCORE.
C
C  SYSTEMS OPTIMIZATION LABORATORY, STANFORD UNIVERSITY.
C  VERSION OF APRIL 1982.
C  *********************************************************************
C
      INTEGER            I, J, NHESS
      INTEGER            MIN0
      DOUBLE PRECISION   ZERO, ONE
      DATA               ZERO/0.0D+0/, ONE/1.0D+0/
C
      WRITE (NOUT, 1000)
      WRITE (NOUT, 1500) CVEC
C
C  PRINT  HESS  UNLESS IT APPEARS TO BE IMPLICIT.
C
      WRITE (NOUT, 2000) NROWH, NCOLH
      IF (NROWH .EQ. 1  .AND.  NCOLH .EQ. 1) GO TO 200
      IF (NCOLH .EQ. 1) WRITE (NOUT, 2100) HESS
      IF (NCOLH .EQ. 1) GO TO 200
      NHESS  = MIN0( NCOLH, N )
      DO 100 J = 1, NHESS
         WRITE (NOUT, 2200) J, (HESS(I,J), I=1,NHESS)
  100 CONTINUE
C
C  CALL  QPHESS  TO COMPUTE EACH COLUMN OF THE HESSIAN.
C
  200 WRITE (NOUT, 3000)
      CALL ZEROVC( N, WRK, N, 1 )
      DO 300 J = 1, N
         WRK(J) = ONE
         CALL QPHESS( N, NROWH, NCOLH, J, HESS, WRK, HX )
         WRITE (NOUT, 3100) J, HX
         WRK(J) = ZERO
  300 CONTINUE
      RETURN
C
 1000 FORMAT(1H1 / 19H OUTPUT FROM QPDUMP / 19H ******************)
 1500 FORMAT(/ 9H CVEC ... / (5G15.6))
 2000 FORMAT(/ 8H NROWH =, I6, 11H    NCOLH =, I6)
 2100 FORMAT(/ 9H HESS ... / (5G15.6))
 2200 FORMAT(/ 7H COLUMN, I6, 14H  OF  HESS ... / (5G15.6))
 3000 FORMAT(// 38H THE FOLLOWING IS RETURNED BY  QPHESS.)
 3100 FORMAT(/ 7H COLUMN, I6, 18H  FROM  QPHESS ... / (5G15.6))
C
C  END OF QPDUMP
      END
