      DOUBLE PRECISION FUNCTION V2NORM( N, X, LENX, INCX )
C
      INTEGER            N, LENX, INCX
      DOUBLE PRECISION   X(LENX)
C
      DOUBLE PRECISION   WMACH
      COMMON    /SOLMCH/ WMACH(15)
C
C  *********************************************************************
C  V2NORM  RETURNS THE EUCLIDEAN NORM OF THE VECTOR X.
C  THE NORM IS COMPUTED BY A ONE-PASS METHOD (DUE TO SVEN HAMMARLING)
C  THAT GUARDS AGAINST OVERFLOW AND (OPTIONALLY) UNDERFLOW.
C
C  SYSTEMS OPTIMIZATION LABORATORY, STANFORD UNIVERSITY.
C  VERSION OF NOVEMBER 1981.
C  *********************************************************************
C
      INTEGER            I, IX
      DOUBLE PRECISION   ABSXI, FLMAX, ONE, Q, RTMIN, SCALE, SQROOT,
     *                   SUMSQ, UNDFLW, ZERO
      DOUBLE PRECISION   DSQRT
      DOUBLE PRECISION   DABS
      DATA               ZERO, ONE/0.0D+0, 1.0D+0/
C
      V2NORM = ZERO
      IF (N .LT. 1) RETURN
      RTMIN  = WMACH(6)
      FLMAX  = WMACH(7)
      UNDFLW = WMACH(9)
C
      IX    = 1
      SCALE = ZERO
      SUMSQ = ONE
      IF (UNDFLW .GT. ZERO) GO TO 130
C
C  NO CHECK FOR UNDERFLOW.
C
      DO 120 I = 1, N
         ABSXI = DABS( X(IX) )
         IF (ABSXI .EQ.  ZERO) GO TO 110
         IF (SCALE .GE. ABSXI) GO TO 100
         SUMSQ = ONE + SUMSQ*(SCALE/ABSXI)**2
         SCALE = ABSXI
         GO TO 110
  100    SUMSQ = SUMSQ + (ABSXI/SCALE)**2
  110    IX    = IX + INCX
  120 CONTINUE
      GO TO 170
C
C  CHECK FOR UNNECESSARY UNDERFLOWS.
C
  130 DO 160 I = 1, N
         ABSXI = DABS( X(IX) )
         IF (ABSXI .EQ. ZERO) GO TO 150
         Q     = ZERO
         IF (SCALE .LT. ABSXI) GO TO 140
         IF (SCALE .GT. RTMIN) Q = SCALE*RTMIN
         IF (ABSXI .GE. Q) SUMSQ = SUMSQ + (ABSXI/SCALE)**2
         GO TO 150
  140    IF (ABSXI .GT. RTMIN) Q = ABSXI*RTMIN
         IF (SCALE .GE. Q) SUMSQ = ONE + SUMSQ*(SCALE/ABSXI)**2
         SCALE = ABSXI
  150    IX    = IX + INCX
  160 CONTINUE
C
  170 SQROOT = DSQRT( SUMSQ )
      V2NORM = FLMAX
      IF (SCALE .LT. FLMAX/SQROOT) V2NORM = SCALE*SQROOT
      RETURN
C
C  END OF V2NORM
      END
