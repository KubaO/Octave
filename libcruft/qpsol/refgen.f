      SUBROUTINE REFGEN( N, ALPHA, X, LENX, INCX, BETA, DELTA )
C
      INTEGER            N, LENX, INCX
      DOUBLE PRECISION   ALPHA, BETA, DELTA
      DOUBLE PRECISION   X(LENX)
C
      DOUBLE PRECISION   WMACH
      COMMON    /SOLMCH/ WMACH(15)
C
C  *********************************************************************
C  REFGEN  GENERATES A HOUSEHOLDER REFLECTION  P  SUCH THAT
C
C     P ( ALPHA )  =  ( DELTA ) ,      P(T) P = I,
C       (   X   )     (   0   )
C
C  WHERE  P  HAS THE FORM
C
C            P  =  ( I   )  -  1/BETA ( BETA ) ( BETA  Z(T) )
C                  (   1 )            (  Z   )
C
C  FOR SOME  BETA  AND  Z,  WHERE  Z  IS A VECTOR WITH  N  ELEMENTS.
C
C  IN CERTAIN CIRCUMSTANCES ( X  VERY SMALL IN ABSOLUTE TERMS OR
C  X  VERY SMALL COMPARED TO  ALPHA),  P  WILL BE THE IDENTITY MATRIX.
C  REFGEN  WILL THEN LEAVE  ALPHA  AND  X  UNALTERED, AND WILL RETURN
C  BETA = ZERO   AND   DELTA = ALPHA.
C
C  OTHERWISE,  REFGEN  RETURNS  BETA  IN THE RANGE (1.0, 2.0),
C  SETS  ALPHA = BETA,  AND STORES  Z  IN THE ARRAY  X.
C  (IN SOME CASES, SETTING  ALPHA = BETA  IS CONVENIENT FOR LATER USE.)
C
C  REFGEN  GUARDS AGAINST OVERFLOW AND UNDERFLOW.
C  IT IS ASSUMED THAT  FLMIN .LT. EPSMCH**2  (I.E.  RTMIN .LT. EPSMCH).
C
C  VERSION 1, MARCH 30 1983.  DERIVED FROM SVEN HAMMARLING'S  NREFG.
C  SYSTEMS OPTIMIZATION LABORATORY, STANFORD UNIVERSITY.
C  *********************************************************************
C
      INTEGER            I, NINCX
      DOUBLE PRECISION   ABSALF, EPSMCH, GAMMA, ONE, RTMIN, TOL, XNORM,
     *                   ZERO
      DOUBLE PRECISION   DABS
      DOUBLE PRECISION   DSQRT, V2NORM
C
      DATA               ZERO/0.0D+0/, ONE/1.0D+0/
C
      BETA   = ZERO
      DELTA  = ALPHA
      IF (N .LT. 1) RETURN
      EPSMCH = WMACH(3)
      RTMIN  = WMACH(6)
C
      ABSALF = DABS(ALPHA)
      XNORM  = V2NORM( N, X, LENX, INCX )
      IF (XNORM   .LE.  RTMIN          ) RETURN
      IF (ABSALF  .LE.  EPSMCH * XNORM ) GO TO 50
      IF (XNORM   .LE.  EPSMCH * ABSALF) RETURN
      GO TO 100
C
C  ALPHA  IS SMALL ENOUGH TO BE REGARDED AS ZERO.
C
   50 DELTA  = XNORM
      BETA   = ONE
      GO TO 200
C
C  NORMAL CASE.
C  WE KNOW THAT   EPSMCH  .LT.  XNORM / ABSALF  .LT.  1/EPSMCH.
C
  100 GAMMA  = DSQRT( ONE  +  (XNORM/ALPHA)**2 )
      DELTA  = ALPHA * GAMMA
      BETA   = ONE + ONE/GAMMA
C
C  SET  X  =  X / DELTA,  WHERE  DABS(DELTA) = NORM( ALPHA, X ).
C  CHANGE NEGLIGIBLE ELEMENTS TO ZERO TO AVOID UNDERFLOW LATER ON.
C
  200 TOL    = DABS( DELTA ) * EPSMCH
      NINCX  = N * INCX
      DO 300 I = 1, NINCX, INCX
         IF (DABS( X(I) ) .LE. TOL) GO TO 250
         X(I)  = X(I) / DELTA
         GO TO 300
C
  250    X(I)  = ZERO
  300 CONTINUE
C
      ALPHA  =   BETA
      DELTA  = - DELTA
      RETURN
C
C  END OF REFGEN
      END
